ENTRY(EntryPoint)
MEMORY
{
    IRAM : ORIGIN = 0x300000, LENGTH = 0x00008000
    ERAM : ORIGIN = 0x20000000, LENGTH = 0x08000000
    LR_FLASH : ORIGIN = 0x20000000, LENGTH = 0x08000000    
}
SECTIONS
{
    ER_FLASH 0x20000400 :
    {
        * (i.EntryPoint)
        * (SectionForBootstrapOperations)
        * (SectionForFlashOperations)
        * (.text*)
        * (i.*)
        * (.rodata*)
        * (rodata)
        * (.constdata*)
        * (.conststring*)
        * (.glue*)
        * (tinyclr_metadata)
        PROVIDE(_sbrk = .);
        PROVIDE(_write = .);
        PROVIDE(_close = .);
        PROVIDE(_fstat = .);
        PROVIDE(_lseek = .);
        PROVIDE(_read = .);
        PROVIDE(_exit = .);
        PROVIDE(_getpid = .);
        PROVIDE(_kill = .);
        PROVIDE(abort = .);
        PROVIDE(__errno = .);
        PROVIDE(_read = .);
        PROVIDE(isatty = .);
        PROVIDE(_isatty = .);
        LONG(0xE12FFF1E); 
    }>LR_FLASH

    ER_RAM_RO 0x20500000 : ALIGN(0x8)
    {        
        *(VectorsTrampolines)
        *(SectionForCodeReadOnlyRAM)
        *(SectionForBootstrapInRam)
    }>ERAM AT>LR_FLASH
    
    ER_RAM_RW 0x20600000:
    {
        * (rwdata)
        * (.data*)
    }>ERAM AT>LR_FLASH
    
    .bss (NOLOAD) : ALIGN(0x8)
    {
        * (.bss*)
        * (.zidata*)
        PROVIDE(__exidx_start = .); 
        PROVIDE(__exidx_end = .); 
        * (COMMON)
    }>ERAM AT>LR_FLASH
    
    /DISCARD/  :
    {
        * (.ARM.exidx*)
        * (.ARM.extab*)
    }    
    
    ER_HEAP_BEGIN 0x22700000 :
    {
        * (SectionForHeapBegin)
    }
    
    ER_HEAP_END 0x26700000 - 0x8 :
    {
        * (SectionForHeapEnd)
    }
    
    ER_RLP_BEGIN 0x26700000 :
    {
        * (SectionForRlpBegin)
    }>IRAM
    
    ER_RLP_END 0x27E00000 - 0x08: 
    {
        * (SectionForRlpEnd)
    }>IRAM
    
    ER_STACK_BOTTOM 0x300030 :
    {
        * (SectionForStackBottom)
    }>IRAM
    
    ER_STACK_TOP 0x306FF0 :
    {
        * (SectionForStackTop +LAST)
    }>IRAM
    
    ER_IRAM_RW 0x307000 :
    {
        *(.SectionForInternalRam.*)
    }>IRAM AT>LR_FLASH

    Load$$SDRAM$$Base = ORIGIN(ERAM);
    Image$$SDRAM$$Length = LENGTH(ERAM);
    Load$$SRAM$$Base = ORIGIN(IRAM);
    Image$$SRAM$$Length = LENGTH(IRAM);

}

Load$$ER_FLASH$$Base = LOADADDR(ER_FLASH);
Image$$ER_FLASH$$Length = SIZEOF(ER_FLASH);
Image$$ER_RAM_RO$$Base = ADDR(ER_RAM_RO);
Image$$ER_RAM_RO$$Length = SIZEOF(ER_RAM_RO);
Load$$ER_RAM_RO$$Base = LOADADDR(ER_RAM_RO);

Image$$ER_IRAM_RW$$Base = ADDR(ER_IRAM_RW);
Image$$ER_IRAM_RW$$Length = SIZEOF(ER_IRAM_RW);
Load$$ER_IRAM_RW$$Base = LOADADDR(ER_IRAM_RW);

Image$$ER_RAM_RW$$Base = ADDR(ER_RAM_RW);
Image$$ER_RAM_RW$$Length = SIZEOF(ER_RAM_RW);
Load$$ER_RAM_RW$$Base = LOADADDR(ER_RAM_RW);
Image$$ER_RAM_RW$$ZI$$Base = ADDR(.bss);
Image$$ER_RAM_RW$$ZI$$Length = SIZEOF(.bss);
Load$$ER_RLP$$Base = LOADADDR(ER_RLP_BEGIN);
Image$$ER_RLP$$Length = LOADADDR(ER_RLP_END) - LOADADDR(ER_RLP_BEGIN);
__use_no_semihosting_swi = 0;
